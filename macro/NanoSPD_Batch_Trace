// NanoSPD_Batch_Trace.txt
// Batch file to export line scans from ImageJ ROI Manager
// Assumes an image with 2 channels.  

// Jonathan Bird, Laboratory of Molecular Genetics
// National Institute on Deafness and Other Communication Disorders, NIH.
// v1.0 - 6/11/2016


//Set decimal place in measurements. 
run("Set Measurements...", "decimal=5");

// Get profile and display values in "Results" window
run("Clear Results");

//Get image info for scaling linesman
getPixelSize(unit, pixelWidth, pixelHeight)

// Read in number of ROIs from ROI manager.
ROI_max = roiManager("count");

//Cycle through each ROI in list
for (i=0; i<ROI_max; i++)
{
	//Select ROI
	roiManager("select", i);
	
	//Select each channel and get profile
	setSlice(1);
	profile1 = getProfile();
	setSlice(2);
	profile2 = getProfile();
  	
	//Copy profiles into results table, append the ROI selection number.  
	for (j=0; j<profile1.length; j++) //Both profiles have the same length 
	{
		setResult("Distance(micron)"+(i+1), j, (j*pixelWidth)); //Assuming pixel is square.
		setResult("ChannelA"+(i+1), j, profile1[j]);
      		setResult("ChannelB"+(i+1), j, profile2[j]);
	}
}

updateResults;

saveAs("Measurements");
